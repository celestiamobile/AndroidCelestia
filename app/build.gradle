/*
 * build.gradle
 *
 * Copyright (C) 2001-2020, Celestia Development Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id "org.jetbrains.kotlin.kapt"
}

android {
    compileSdkVersion constants.compileSdkVersion
    buildToolsVersion constants.buildToolsVersion
    ndkVersion constants.ndkVersion

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.8"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        applicationId "space.celestia.mobilecelestia"
        minSdkVersion constants.minSdkVersion
        targetSdkVersion constants.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    externalNativeBuild {
        cmake {
            version constants.cmakeVersion
            path "src/main/cpp/crash/CMakeLists.txt"
        }
    }

    task copyLocalizedFiles(type: Exec) {
        println "Copying localized files"
        workingDir projectDir
        executable = '/bin/sh'
        args = ["copy_localized_files.sh"]
    }

    task copyGeneralData(type: Exec) {
        println "Copying general data"
        workingDir projectDir
        executable = '/bin/sh'
        args = ["copy_general_data.sh"]
    }

    task convertPO(type: Exec) {
        println "Converting PO"
        workingDir projectDir
        executable = '/bin/sh'
        args = ["convert_po.sh"]
    }
    namespace 'space.celestia.mobilecelestia'

    copyGeneralData.mustRunAfter copyLocalizedFiles
    convertPO.mustRunAfter copyGeneralData
    preBuild.dependsOn copyLocalizedFiles
    preBuild.dependsOn copyGeneralData
    preBuild.dependsOn convertPO
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$core_appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.recyclerview:recyclerview:1.3.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.6.0"

    implementation "com.google.android.material:material:$material_version"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"

    implementation "net.lingala.zip4j:zip4j:2.11.5"

    implementation "androidx.webkit:webkit:1.7.0"

    implementation project(path: ':LinkPreview')
    implementation project(path: ':Celestia')

    def appCenterSdkVersion = '5.0.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    def composeBom = platform('androidx.compose:compose-bom:2023.06.01')
    implementation composeBom
    androidTestImplementation composeBom

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "com.google.accompanist:accompanist-themeadapter-material3:0.30.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}
